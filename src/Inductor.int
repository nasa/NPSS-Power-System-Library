/***
 -------------------------------------------------------------------------------
 |                                                                             |
 | NASA Glenn Research Center                                                  |
 | 21000 Brookpark Rd 		                                                     |
 | Cleveland, OH 44135 	                                                       |
 |                                                                             |
 | File Name:     Inductor.int										                             |
 | Author(s):     Jeffrey Csank, David Sadey, Tom Lavelle                      |
 | Date(s):       April 2018	                                                 |
 |                                                                             |
 -------------------------------------------------------------------------------
***/

#ifndef __INDUCTOR__
#define __INDUCTOR__

class Inductor extends ElectricElement {
  
  //----------------------------
  // ****** DOCUMENTATION ******
  //----------------------------  

  // title = "";
  description = "The " + isA() + " model represents a inductor with a fixed inductance.";

  usageNotes = isA() +
  "

- The input design parameters that must be specified by the modeler when creating an instance 
of this " + isA() + " component is inductance, H, in Henries.

- This component supports DC and AC (single or multiphase) power (see ElectricPowerType). 
At instantiation, this component's power type is unspecified. NPSS Power System Components 
need to have a power type in order to correctly run power calculations. For this component, 
the power type is typically set by calling the findSourcesAndPropagate() method at the top 
level assembly. This method is defined in ElectricPort.prt.

- Note that, as an inductor, this component will act as an short circuit to DC. Because this 
component does not include dynamics, expect it to act as a short in DC systems.

- This component does not contain a node, and so starts each iteration with its port voltages 
unknown. The concept in NPSS Power System Library is that all components that contain nodes 
all have a prePass() method specified that pushes node voltage information, via their ports
to all components that do not contain nodes and so do not know their voltages. The intent is 
that after all component prePass methods are automatically called, all components in the 
system will then have port voltages specified. Then during the normal iteration, components 
that don't contain nodes such as this one, will execute first. These components typically 
transmit power from one node to another and do not transform it from one kind to another 
(e.g. cables and breaker). These execute first in the solver sequence, and inside their 
calculate() methods, they calculate their currents, which are then passed via the ports, to 
all other components in the circuit. So after these nodeless transmission components 
execute, all electric port voltages and currents should be specified, and so all other 
components are free to run.

- This component is a circuit component and, at this time, has not been set up to include 
thermal models.
 
- Solver independents and dependents are not used in the " + isA() + " component, and there is 
no sizing logic included in this component.";
  
  
  //------------------------------
  // ****** SETUP VARIABLES ******
  //------------------------------
  
  real L;	
  ComplexNumber Z;
  ComplexNumber dV;
  ComplexNumber I;
  
  //----------------------------------------------------------
  // ****** SETUP PORTS, FLOW STATIONS, SOCKETS, TABLES ******
  //----------------------------------------------------------  
  
  ElectricInputPort EP_I {
    description = "Electric input port.";
  }

  ElectricOutputPort EP_O {
    description = "Electric output port.";
  }
  string EPortRef;
  
  void calculate() {
    
    // calculate current from voltage drop
    dV.copy( "EP_I.V" );
    dV.minus( "EP_O.V" );	
    Z.setrj( 0., 2.*PI*EP_I.frequency*L );
    I.copy( "dV" );
    I.div( "Z" );

    // set the conditions in the ports
    EP_O.setIVRMS( I.rRMS, I.jRMS, EP_O.V.rRMS, EP_O.V.jRMS ); 	
    EP_I.setIVRMS( I.rRMS, I.jRMS, EP_I.V.rRMS, EP_I.V.jRMS ); 	
    EP_O.frequency=EP_I.frequency;
  }
}
#endif
