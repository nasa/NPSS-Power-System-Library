/***
 -------------------------------------------------------------------------------
 |                                                                             |
 | NASA Glenn Research Center                                                  |
 | 21000 Brookpark Rd 		                                                   |
 | Cleveland, OH, 44135 	                                                   |
 |                                                                             |
 | File Name:     Rectifier.int										           |
 | Date(s):       March 2019                                                   |
 | Author(s):     Jeffrey Csank, David Sadey, Tom Lavelle                      |
 |                                                                             |
 -------------------------------------------------------------------------------
***/

#ifndef __RECTIFIER__
#define __RECTIFIER__

//#include <InterpIncludes.ncp>
class Rectifier extends Element {
	
	//------------------------------------------------------------
	//     ******* DOCUMENTATION *******
	//------------------------------------------------------------
	
	title = "";
	
	description = isA() + " Determines power for a Rectifier";
	
	usageNotes = isA() + "- NOTE TO USERS: This file....";
	
	background = "";
	
	//------------------------------------------------------------
	//     ******* SETUP VARIABLES ********
	//------------------------------------------------------------
	
	ComplexNumber Sdc;
	ComplexNumber Idc;
	ComplexNumber Iac;
	ComplexNumber Vdc;
		
	real eff {
		value = 1.0; IOstatus="input"; units= NONE;
		description = "Efficiency of the rectifier";
	}

	real effBase {
		value = 1.;  IOstatus = "output";  units = "none";
		description = "Adiabatic efficiency.  Calculated and set by S_map socket during off design.";
	}

	real effDes {
		value = 1.;  IOstatus = "input";  units = "none";
		description = "Adiabatic efficiency at design point.";
	}
	
	real frequency {
		value = 60.; IOstatus="input"; //units=;
		description = "Frequency of AC output power";
	}

	real pwrDes {
		value = 1.;  IOstatus = "input";  units = "kW";
		description = "Output power at design";
	}

	real pwrOut {
		value = 1.;  IOstatus = "output";  units = "kW";
		description = "Output power";
	}

	real Vimag {
		value = -1.; IOstatus="input"; //units=;
		description = "Rectifier Voltage (imaginary)";
	}
	
	real Vreal {
		value = 1000.; IOstatus="input"; //units=;
		description = "Rectifier Voltage (real)";
	}
	
	real SpecificPower {
		value = 13; IOstatus="input"; //units=KW/KG;
		description = "Power to Weight ratio for the component";
	}

	real Mass {
		value=1.; IOstatus="output"; units="kg";
		description = "Component mass";
	}

	real Loss_j {
		value = 0; IOstatus="output"; units="kW";
		description = "Generator Loss";
	}

	real Loss_r {
		value = 0; IOstatus="output"; units="kW";
		description = "Generator Loss";
	}

    real Q_heat {
        value = 0; IOstatus="output"; //units=W;
        description = "Power dissipation at current time";
    }

	
	//------------------------------------------------------------
    //   ******* OPTION VARIABLE SETUP *******
    //------------------------------------------------------------

    Option switchDes {
		allowedValues = { "DESIGN", "OFFDESIGN" } ;
		description = "Determines if the element is in design or off-design mode";
		rewritableValues = FALSE;
	}
    Option switchThermPort {
    	allowedValues = { "TRUE", "FALSE" };
        //value = "FALSE";
    	description = "Determines if component needs thermal port";
    	rewritableValues = FALSE;  // Enables converter optimization.
        trigger=TRUE;
    }

	

	//------------------------------------------------------------
	// ****** SETUP PORTS, FLOW STATIONS, SOCKETS, TABLES ********
	//------------------------------------------------------------

	// FLUID PORTS

    // FUEL PORTS

    // BLEED PORTS
     
    // THERMAL PORTS

    //ELECTRICAL PORTS

	ElectricPort EP_O {
		description = "Electric port";
	}
	
	ElectricPort EP_I {
		description = "Electric port";
	}

	// MECHANICAL PORTS

    // FLOW STATIONS

    // SOCKETS

    Socket S_map{
		allowedValues = {"effBase"}
		description = "Socket to calculate motor map performance";
		//socketType = "INVERTER_RECTIFIER_MAP";
		socketType = "TURBINE_EFFICIENCY_MAP";
	}

    Socket S_eThermMass {
		allowedValues = { "Q_heat", "Mass" };
		description = "Thermal mass socket";
		socketType = "EThermalMass";
    }

    // TABLES

	
	//------------------------------------------------------------
	//  ******  ADD SOLVER INDEPENDENTS & DEPENDENTS  ******
	//------------------------------------------------------------
	Independent ind_Vreal { 
		varName = "Vreal"; 
		autoSetup = TRUE;
		indepRef = "1000";
		
		description = "Varies the real component of the inverter voltage";
	}
	
	Independent ind_Vimag { 
		varName = "Vimag"; 
		autoSetup = TRUE;
		indepRef = "1000";
		description = "Varies the imaginary component of the inverter voltage";
	}
	
	Dependent dep_Power{
		eq_rhs="EP_I.S.rRMS * eff";
		eq_lhs="EP_O.S.rRMS";
		autoSetup=TRUE;
	}
	
	Dependent dep_phase{
		eq_rhs="EP_I.VLN.phaseDeg";
		eq_lhs="EP_I.I.phaseDeg";
		autoSetup=TRUE;
	}

	//------------------------------------------------------------
	// ******* VARIABLE CHANGED METHODOLOGY *******
	//------------------------------------------------------------

	void variableChanged(string name, any oldVal){
		if ( name == "switchThermPort" ){ 
			if ( switchThermPort == "TRUE" ){
				create("", "ThermalInputPort", "Q_I" );
			}
		}
		if(name == "switchDes"){
			if(switchDes == OFFDESIGN){
				effDes.IOstatus = "output";
			}
		}
	} // end variableChanged()

	//------------------------------------------------------------
	//   ******* PERFORM ENGINEERING CALCULATIONS *******
	//------------------------------------------------------------

	void calculate() {
		
		pwrOut = EP_O.S.rRMS;
		
		if (switchDes == "DESIGN" ){
			/****************************
			On-design
			****************************/
			// Handle case where guessed design power is zero.
			if (abs(pwrOut) < 1e-4) { pwrOut = 1e-4; }
			pwrDes = pwrOut;
			
			// Compute mass based on assumed specific power.  Both Mass
			// and loses are based on output power (Power Sent)
			Mass = pwrOut / SpecificPower;
			
		}
		else{
			//*****************************
			// Off-Design
			//*****************************
		}	

		//set efficiency value
		if(!S_map.isEmpty()){
			S_map.execute();
		}
		else{
			effBase = effDes;
		}
		eff = effBase;
		
        Loss_r = EP_I.S.r - EP_O.S.r;
        Loss_j = EP_I.S.j - EP_O.S.j;

        if (switchThermPort=="TRUE"){
            real KW_PER_BTU_PER_SEC = 1.05505585;
            Q_heat = sqrt(Loss_r*Loss_r + Loss_j*Loss_j);
            Q_heat /= KW_PER_BTU_PER_SEC;
            //------------------------------------------------------------    
            // Run the thermal mass model.
            //------------------------------------------------------------
            if ( !S_eThermMass.isEmpty() ) {
              S_eThermMass.execute();
            }
        }
	}
	
	//------------------------------------------------------------
	// ******* PREPASS FUNCTION *******
	//------------------------------------------------------------
	void prePass() {	
		EP_I.frequency = frequency;
		EP_I.setIVRMS(EP_I.I.r, EP_I.I.j, Vreal, Vimag);
		EP_O.setIVRMS(EP_O.I.r, EP_O.I.j, EP_I.V.mag * 3 * sqrt(2) / PI, 0);
	}
}
#endif