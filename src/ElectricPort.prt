
#include "ComplexNumber.int"

class ElectricPort extends Subelement {
	
	//------------------------------------------------------------
	//     ******* DOCUMENTATION *******
	//------------------------------------------------------------
	title = "";
	description = isA() + " Electric Port";
	usageNotes = isA() + "- NOTE TO USERS: This file....";
	background = "";   
	
	//-------------------------------------------------
	// Electric port parameters
	//-------------------------------------------------
	ComplexNumber V{
		lock = 1;	
	}
	ComplexNumber I{
		lock = 1;	
	}
	ComplexNumber S{
		lock = 1;
    }
    
    FunctVariable Vdc {
        getFunction = "getVdc";
        hide( 1 );
    }
    
    real getVdc() {
    	return V.r;	
    }
    
    FunctVariable Idc {
        getFunction = "getIdc";
        hide( 1 );
    }   
    real getIdc() {
    	return I.r;	
    }
    
    FunctVariable Pdc {
        getFunction = "getPdc";
        hide( 1 );
    }   
    real getPdc() {
    	return S.r;	
    }
    Pdc.hide( 0 );
    Idc.hide( 0 );
    Vdc.hide( 0 );
    V.hide( 1 );
    S.hide( 1 );
    I.hide( 1 );
    
	socketRequired = 0;
	
	Option ElectricPowerType {
		allowedValues = { "AC1", "DC", "AC3" };
		description = "Electric Power Type (DC, AC1, AC3)";
		//IOstatus = 'INPUT';
		trigger = 1;
		rewritableValues = FALSE;
	}
	
	real frequency{
		value=0.; 
		IOstatus="input"; //units=kW;
		description = "Frequency";
	}
	
	real PF{
		value=0;
		description = "Ratio of real to apparent power";
	}
	
	string refport{
		description = "port used to copy from";	
	}
	
	//------------------------------------------------------------
	// ******* VARIABLE CHANGED METHODOLOGY *******
	//------------------------------------------------------------
	void variableChanged( string name, any oldVal ) {
		
		if ( name =="ElectricPowerType" ) {
			// change variable IO status depending switchDes status
			if ( ElectricPowerType == "DC" ) {
				Pdc.hide( 0 );
				Idc.hide( 0 );
				Vdc.hide( 0 );
				V.hide( 1 );
				S.hide( 1 );
				I.hide( 1 );
				frequency=0;
			} 
			else if ( ElectricPowerType == "AC1" ) {
				Pdc.hide( 1 );
				Idc.hide( 1 );
				Vdc.hide( 1 );
				V.hide( 0 );
				S.hide( 0 );
				I.hide( 0 );		
			} 
			else if ( ElectricPowerType == "AC3" ) {
				Pdc.hide( 1 );
				Idc.hide( 1 );
				Vdc.hide( 1 );
				V.hide( 0 );
				S.hide( 0 );
				I.hide( 0 );		
			} 
		} 
	} 
	
	
	//------------------------------------------------------
	// copy the port information
	//------------------------------------------------------
	void copyport() {
		V.copy( refport+".V" );
		I.copy( refport+".I" );
		S.copy( refport+".S" );
		
		frequency=refport->frequency;
		if (refport==""){
			cerr<<"Hey I need a refport!!!"<<endl;
		}		
	}
	
	
	
	//------------------------------------------------------
	// push the port information
	//------------------------------------------------------
	void pushport() {
		refport->copyport();
		if (refport==""){
			cerr<<"Hey I need a refport!!!"<<endl;
		}		
	}
	
	//-----------------------------------------------------------
	// set the port conditions based on current, voltage, 
	// and angles in degrees
	//-----------------------------------------------------------
	void setIVphaseDeg( real Imag, real Iangle, real Vmag, real Vangle ){
		setIV( Imag*cos(Iangle*PI/180.),	Imag*sin(Iangle*PI/180.),
			Vmag*cos(Vangle*PI/180.),	Imag*sin(Vangle*PI/180.) );
	}
	
	//-----------------------------------------------------------
	// set the port conditions based on current, voltage, and 
	// angles in radians
	//-----------------------------------------------------------
	void setIVphaseRad( real Imag, real Iangle, real Vmag, real Vangle ){
		setIV( Imag*cos(Iangle),	Imag*sin(Iangle),
			Vmag*cos(Vangle),	Imag*sin(Vangle) );
	}
	
	
	ComplexNumber VLN{
		hide( 1 );	
	}
	//-----------------------------------------------------------
	// set the port conditions based on complex current and voltage
	//-----------------------------------------------------------
	void setIV( real Ir, real Ij, real Vr, real Vj ){
		
		//--------------------------------------------------------------------
		// unlock the parameters so they can be set
		//--------------------------------------------------------------------
		V.lock = 0;
		I.lock = 0;
		S.lock = 0;
		
		// set the current and voltage
		V.setrj( Vr, Vj );
		I.setrj( Ir, Ij );
		
		// those are rms values since this is an electrical port
		// change to peak
		V.scale( sqrt( 2. ));
		I.scale( sqrt( 2. ));
		
		//--------------------------------------------------------------------
		// check to see if we are in DC power mode and passing imaginary info
		//--------------------------------------------------------------------
		if (ElectricPowerType == "DC" ){	
			if ( Vj!=0. || Ij!=0. ){
				cerr << "you are running a DC circuit with imaginary comps\n";	
			}
		}
		
		//-------------------------------------------------------------------
		// determine the power
		//--------------------------------------------------------------------
		if (ElectricPowerType == "DC" || ElectricPowerType=="AC1")
		{
			S.copy( "I" );
			S.conjugate();
			S.timesRMS( "V" );
			S.scale( 1./1000. );
		}
		
		if (ElectricPowerType == "AC3"){
			S.copy( "I" );
			S.conjugate();
			S.timesRMS( "V" );
			S.scale( 3./1000. );
			
			VLN.copy( "V" );
			VLN.setMagPhase( V.mag, V.phase-30.*PI/180. );
			VLN.scale( sqrt(1./3.) );
			S.copy( "I" );
			S.conjugate();
			S.times( "VLN" );
			S.scale( .5 );
			S.scale( 3./1000. );
			
		}
		
		PF = S.r/S.mag;
		
		//------------------------------------------------------------------
		// lock the numbers again
		// only way to set the values should be through this function
		//------------------------------------------------------------------
		V.lock = 1;
		I.lock = 1;
		S.lock = 1;
		
		pushport();
		
	}
	
}



class ElectricOutputPort extends ElectricPort;

class ElectricInputPort extends ElectricPort;