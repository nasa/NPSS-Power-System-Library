/***
 -------------------------------------------------------------------------------
 |                                                                             |
 | NASA Glenn Research Center                                                  |
 | 21000 Brookpark Rd 		                                                     |
 | Cleveland, OH 44135 	                                                       |
 |                                                                             |
 | File Name:     ElectricPort.prt											                       |
 | Author(s):     Jeffrey Csank, David Sadey, Tom Lavelle, George Thomas,      |
 |                Jonathan Fuzaro Alencar, Michael Stich                       |
 | Date(s):       December 2019, March 2023                                    |
 |                                                                             |
 -------------------------------------------------------------------------------
***/

#ifndef __ELECTRIC_PORT__
#define __ELECTRIC_PORT__

#include "ComplexNumber.int"

int ESO_Id_NoRefPort = (10000 * 141) + (100 * 22) + 1;
ESOregCreate(ESO_Id_NoRefPort, 1, "No refport in this port (make sure the port is connected)!");
int ESO_Id_ImagInDC = (10000 * 142) + (100 * 22) + 1;
ESOregCreate(ESO_Id_ImagInDC, 1, "Imaginary components in DC port!");

string sourceComponents[] = { "Source", "Generator", "Battery" };
string loadComponents[] = { "Motor", "ConstantPowerLoad" };
string converterComponents[] = { "Inverter", "Rectifier", "DC_DC_Converter" };

class ElectricPort extends Subelement {
  
  //------------------------------
  // ****** DOCUMENTATION ********
  //------------------------------  

  // title = "";
  // description = "The " + isA() + "";
  // usageNotes = isA() + "- NOTE TO USERS: This file....";
  // background = "";
  
  //--------------------------------
  // ****** SETUP VARIABLES ********
  //--------------------------------

  #include "PropagatePower.fnc"

  socketRequired = 0;

  ComplexNumber V {
    lock = TRUE;	
  }

  ComplexNumber I {
    lock = FALSE;	
  }

  ComplexNumber S {
    lock = TRUE;
  }
  
  V.hide(1);
  S.hide(1);
  I.hide(1);
  
  Option ElectricPowerType {
    allowedValues = { "UNDEF", "AC1", "DC", "AC3" };
    description = "Electric power type (DC, AC1, AC3).";
    trigger = 1;
    rewritableValues = TRUE;
  }
  
  real frequency {
    value = 0.;
    IOstatus = "input";
    description = "Frequency of AC current through port.";
  }
  
  real PF {
    value = 0;
    description = "Ratio of real to apparent power.";
  }
  
  string refport {
    description = "Port used to copy from.";	
  }
  
  ComplexNumber VLN {
    hide(0);	
  }
  
  //---------------------------------------------
  // ******* VARIABLE CHANGED METHODOLOGY *******
  //---------------------------------------------
  void variableChanged(string name, any oldVal) {

    // change variable IO status depending switchDes status
    if (name == "ElectricPowerType") {
      if (ElectricPowerType == "DC") {
        V.hide(0);
        S.hide(0);
        I.hide(0);
        frequency = 0;
      } else if (ElectricPowerType == "AC1") {
        V.hide(0);
        S.hide(0);
        I.hide(0);		
      } else if (ElectricPowerType == "AC3") {
        V.hide(0);
        S.hide(0);
        I.hide(0);		
      } 
    } 
  } 
  
  // copy the port information
  void copyport() {
    V.copy(refport + ".V");
    I.copy(refport + ".I");
    S.copy(refport + ".S");
    VLN.copy(refport + ".VLN");
    
    if (refport->frequency == 0) {
      refport->frequency = frequency;
    } else {
      frequency = refport->frequency;
    }

    PF = refport->PF;

    if (refport == "") {
      ESOreport(ESO_Id_NoRefPort, "Ref port empty!");
    }		
  }
  
  //------------------------------------------------------
  // push the port information
  //------------------------------------------------------
  void pushport() {
    if (refport == "") {
      ESOreport(ESO_Id_NoRefPort, "Ref port empty!");
    } else {
      refport->copyport();
    }
  }
  
  //-----------------------------------------------------------
  // set the port conditions based on current, voltage, 
  // and angles in degrees
  //-----------------------------------------------------------
  void setIVRMSphaseDeg(real ImagRMS, real Iangle, real VmagRMS, real Vangle) {
    setIVRMS(ImagRMS * cos(Iangle * PI/180.), ImagRMS * sin(Iangle * PI/180.),
             VmagRMS * cos(Vangle * PI/180.), VmagRMS * sin(Vangle * PI/180.));
  }
  
  // set the port conditions based on current, voltage, and angles in radians
  void setIVRMSphaseRad(real ImagRMS, real Iangle, real VmagRMS, real Vangle) {
    setIVRMS(ImagRMS * cos(Iangle), ImagRMS * sin(Iangle),
             VmagRMS * cos(Vangle), ImagRMS * sin(Vangle));
  }
  
  // set the port conditions based on complex current and voltage
  void setIVRMS(real IrRMS, real IjRMS, real VrRMS, real VjRMS) {
    
     if (ElectricPowerType == "UNDEF") {
      cerr << "[ERROR]: " << parent.getPathName() << " undefined!\n";
      quit();
    }
       
    // unlock the parameters so they can be set
    V.lock = FALSE;
    I.lock = FALSE;
    S.lock = FALSE;
    
    // set the current and voltage
    V.setrjRMS(VrRMS, VjRMS);
    I.setrjRMS(IrRMS, IjRMS);
    
    // check to see if we are in DC power mode and passing imaginary info
    if (ElectricPowerType == "DC") {	
      if (VjRMS != 0. || IjRMS != 0.) {
        ESOreport(ESO_Id_ImagInDC, refport + " contains imaginary components!", 1);
      }
    }
    
    // determine the power
    if (ElectricPowerType == "DC" || ElectricPowerType == "AC1") {

      // VLN = V (VLL) for single phase AC
      VLN.copy("V");
      
      // calculate power
      S.copy("I");
      S.conjugate();
      S.times("V");
      S.scale(1. / 1000.);
    } else if (ElectricPowerType == "AC3") {
      
      // calculate VLN
      VLN.setMagPhase( V.mag * sqrt(1. / 3.), V.phase - (30. * PI/180.));

      // calculate power based off VLN (since VLN is already scaled)
      S.copy("I");
      S.conjugate();
      S.timesRMS("VLN");
      S.scale(3. / 1000.);
    }
    
    // calculate power factor
    PF = S.r / S.mag;
    
    // lock the numbers again
    // only way to set the values should be through this function
    V.lock = TRUE;
    I.lock = FALSE;
    S.lock = TRUE;
    
    pushport();	
  }
}

class ElectricInputPort extends ElectricPort;
class ElectricOutputPort extends ElectricPort;

// Component list produced by crawling over the circuit graph in propagatePower();
// may be used by viewer files, etc.
string powerComponentListSourceToLoad[];
// defaultElectricalSolverSequence contains the strings that will be passed into solverSequence
string defaultElectricalSolverSequence[];
// EnodesInDesign contains all the Enodes in the design and their relative input components
string EnodesInDesign[][];

// trimList extracts the names of each component in the provided string array, and returns a string array with the trimmed strings for each index
string[] trimList(string list[]) {
  int i;
  int size = list.entries();
  for (i = 0; i < size; i++) {
    list[i] = list[i]->getName();
  }
  return list;
}

// propagateEpsSolverListAndPowerTypes finds valid defined sources and propagates their power type
// propagateEpsSolverListAndPowerTypes appends the defaultElectricalSolverSequence array with the right components to be passed into solverSequence
string[] propagateEpsSolverListAndPowerTypes() {
  string portComponent;
  string ports[] = list("ElectricOutputPort", TRUE);
  string Enodes[] = list("Enode", TRUE);
  Enodes = trimList(Enodes);
  int designEntries = ports.entries();

  // The following loop appends each Enode to a different axis in the 2d array
  // | Enode1 | Enode2 | Enode3 |
  // | Comp I | Comp I | Comp I |
  int i;
  int enodesCount = Enodes.entries();
  for (i = 0; i < enodesCount; i++) {
    string clone = Enodes[i];
    string cloneArray[];
    if(cloneArray.entries() == 0){cloneArray.append(clone);}
    cloneArray[0] = clone;
    EnodesInDesign.append(cloneArray);
  }
  
  for (i = 0; i < designEntries; i++) {
    portComponent = ports[i]->parent.isA();
    //search for undefined power source ports and propagate their subgraph
    if (sourceComponents.contains(portComponent)) {
      if (ports[i]->ElectricPowerType != "UNDEF") {
        //Calls recursive function scanDesign populates the EnodesInDesign 2d array with all the component inputs for the Enodes
        ports[i]->scanDesign();
        ports[i]->propagatePower();
      }
      else {
        cerr << "\n[ERROR]: " << ports[i]->parent.getPathName()
             << " is a source with undefined power type!\n";
      }
    }
  }
  //cout << EnodesInDesign << endl;
  for (i = 0; i < designEntries; i++) {
    portComponent = ports[i]->parent.isA();
    if (sourceComponents.contains(portComponent)) {
      if (ports[i]->ElectricPowerType != "UNDEF") {
        // temp variable gets the source name and appends it to the defaultElectricalSolverSequence array
        string current = ports[i]->parent.getPathName();
        // The following code trims the current source instance of ports[i]
        current = current->getName();
        defaultElectricalSolverSequence.append(current);
        // Calls recursive function crawlThroughDesign that appends all components (except Enodes with more than 1 input) to the defaultElectricalSolverSequence array
        ports[i]->crawlThroughDesign();
      }
    }
  }

  // The following code checks if all componenets passed into propagateEpsSolverListAndPowerTypes() are in the return string array
  for (i = 0; i < designEntries; i++) {
    ports[i] = ports[i]->parent.getName();
    
    //If the defaultElectricalSolverSequence string array doesn't contain every ports component then the function could not reach all components
    if (!(defaultElectricalSolverSequence.contains(ports[i]))) {
      cerr << "\n[WARNING]: propagateEpsSolverListAndPowerTypes() could not find all components in design, solverSequence was not set up. Navigate to power_propagation.mdl for an example on how to manually set up solverSequence.\n";
      cerr << "Port order created by propagateEpsSolverListAndPowerTypes(): " << endl << defaultElectricalSolverSequence << endl;
      cerr << "Ports passed into propagateEpsSolverListAndPowerTypes(): " << endl << ports << endl;
      string voidArr[];
      return voidArr;
    }
  }
  //cout << defaultElectricalSolverSequence << endl;
  return defaultElectricalSolverSequence;
}

#endif
