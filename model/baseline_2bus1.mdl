//-------------------------------------------------------------------------------------------------
//
//   File Name:     power_train.mdl
//   Date(s):       April 19, 2018
//   Author:        Jeffrey Csank, David Sadey, Tom Lavelle
//					NASA Glenn Research Center
//
//   Description:   Model file describes model for system that uses 2 generators and combines power 
//					to one motor.
//
//-------------------------------------------------------------------------------------------------

#include "ElectricPort.prt"

cout<<"======================================="<<endl;
cout<<" Model with Bus with 2 Inputs / 1 Output "<<endl;
cout<<"======================================="<<endl;

setThermoPackage("GasTbl");

/*******************************************
 Branch 1 
 Gen1a -> Cable1a
					-> Bus1 -> Cable1 -> EM1
 Gen1b -> Cable1b
*******************************************/

Element eBasicGenerator Gen1a {
	Vbus_LL=1200;
	Eff=0.96;
	SpecificPower=13.;
	 void preexecute(){
	 	Current = GeneratorPower/Vbus_LL/2;
	 };
}

Element eGenericCable Cable1a {
	LineResistance=4.3245e-6/2;
	SpecificAmperage=170./2;
	CableLength=28.3464/2; 
}

Element eBasicGenerator Gen1b {
	Vbus_LL=1200;
	Eff=0.96;
	SpecificPower=13.;
	 void preexecute(){
		Current = GeneratorPower/Vbus_LL/2;
	 };
}

Element eGenericCable Cable1b {
	LineResistance=4.3245e-6/2;
	SpecificAmperage=170./2;
	CableLength=28.3464/2; 
}

Element Bus2to1 Bus1 {
}

Element eGenericCable Cable1 {
	LineResistance=4.3245e-6/2;
	SpecificAmperage=170./2;
	CableLength=28.3464/2; 
}

 Element eBasicMotor EM1 {
	 Eff=0.96;
	 SpecificPower=13.0; 
	 Torque=55144.0; 
}

/*******************************************
 Branch 2
 Gen2 -> Cable2 -> EM2
 *******************************************/
Element eBasicGenerator Gen2 {
	Vbus_LL=1200;
	Eff=0.96;
	SpecificPower=13.;
	 void preexecute(){
		Current = GeneratorPower/Vbus_LL;
	 };
}

Element eGenericCable Cable2 {
	LineResistance=4.3245e-6;
	SpecificAmperage=170.;
	CableLength=28.3464; 
}

Element eBasicMotor EM2 {
	 Eff=0.96;
	 SpecificPower=13.0; 
	 Torque=55144.0;   // in*lb ?????
}

/**************************
 Electric Motor Connections
**************************/
Element FlowStart Atm{
	Pt=14.696;
	Tt=518.67;
	W=4394.74/10;
}

Element Compressor Prop{
  #include "fanE3.map";
	PRdes=1.4;
	effDes=0.93;
}

Element FlowEnd dump{
};

Element Shaft Propulsor_Shaft {
	Nmech=4000;   // rpm
	ShaftInputPort Sh1_I, Sh2_I, Sh_Prop;
}

/**************************
 Turbine Engine / Generator  Connections
**************************/
Element FlowStart TurbineAtm{
	Pt=500;
	Tt=2200;
	W=500;
	FAR = .02;
}

Element Turbine Turb{
	#include "lptE3.map";	
	PRbase=2;
	effDes=0.9;
}

Nozzle Noz{
  PsExh = 14.;
}

Element FlowEnd Turbinedump{
};

real GeneratorPower = 2.6e3;

Independent ind_genPower{
	varName="GeneratorPower";
	autoSetup=TRUE;
};
	

Independent turbPR{
	varName="Turb.PRdes";
};

Dependent VbusIn{
	eq_lhs = "Bus1.EP_Ia.Voltage";
	eq_rhs = "Bus1.EP_Ib.Voltage";
	autoSetup=TRUE;
}

Independent VbusOut{
	varName = "Gen1a.Vbus_LL";
	autoSetup=TRUE;
}

Element Shaft Turbine_Shaft {
	Nmech=4000;   // rpm	
	ShaftInputPort Sh1_Ia, Sh1_Ib, Sh2_I, Sh_Turbine;	
}

// temporary issue with electric port
EM1.EP_I.setOption("ElectricPowerType","AC3");
EM2.EP_I.setOption("ElectricPowerType","AC3");
Cable1.EP_O.setOption("ElectricPowerType","AC3");
Cable1.EP_I.setOption("ElectricPowerType","AC3");
Cable1a.EP_O.setOption("ElectricPowerType","AC3");
Cable1a.EP_I.setOption("ElectricPowerType","AC3");
Cable1b.EP_O.setOption("ElectricPowerType","AC3");
Cable1b.EP_I.setOption("ElectricPowerType","AC3");
Cable2.EP_O.setOption("ElectricPowerType","AC3");
Cable2.EP_I.setOption("ElectricPowerType","AC3");
Gen1a.EP_O.setOption("ElectricPowerType","AC3");
Gen1b.EP_O.setOption("ElectricPowerType","AC3");
Gen2.EP_O.setOption("ElectricPowerType","AC3");
Bus1.EP_O.setOption("ElectricPowerType","AC3");
Bus1.EP_Ia.setOption("ElectricPowerType","AC3");
Bus1.EP_Ib.setOption("ElectricPowerType","AC3");

void linkPortE( string E1, string E2 ){
  E1->refport = E2;
  E2->refport = E1;
}

//-------------------------------------------------------------------------------------------------
// Component Linkages
//-------------------------------------------------------------------------------------------------
linkPorts( "Propulsor_Shaft.Sh1_I"  , "EM1.Sh_O"          	, "Shaft_Motor1" );
linkPortE( "Gen1a.EP_O", "Cable1a.EP_I" );
linkPortE( "Gen1b.EP_O", "Cable1b.EP_I" );
linkPortE( "Cable1a.EP_O","Bus1.EP_Ia" );
linkPortE( "Cable1b.EP_O","Bus1.EP_Ib" );
linkPortE( "Bus1.EP_O","Cable1.EP_I" );
linkPortE( "Cable1.EP_O", "EM1.EP_I" );
linkPorts( "Propulsor_Shaft.Sh2_I"  , "EM2.Sh_O"          	    , "Shaft_Motor2" );
linkPortE( "Gen2.EP_O", "Cable2.EP_I" );
linkPortE( "Cable2.EP_O", "EM2.EP_I" );
linkPorts( "Atm.Fl_O"               , "Prop.Fl_I"           , "St0-St1");

// if not using nozzle
//linkPorts( "Prop.Fl_O"              , "dump.Fl_I"           , "St1-End");

// using nozzle
linkPorts( "Prop.Fl_O"              , "Noz.Fl_I"           ,  "St1-St2");
linkPorts( "Noz.Fl_O"              ,  "dump.Fl_I"           , "St2-End");

//
linkPorts( "Prop.Sh_O"              , "Propulsor_Shaft.Sh_Prop" , "Prop-to-Shaft");
linkPorts( "TurbineAtm.Fl_O"        , "Turb.Fl_I"           , "St00-St10");
linkPorts( "Turb.Fl_O"              , "Turbinedump.Fl_I"    , "St10-End0");
linkPorts( "Turb.Sh_O"        		, "Turbine_Shaft.Sh_Turbine" , "Turb-to-Shaft");
linkPorts( "Turbine_Shaft.Sh1_Ia"    , "Gen1a.Sh_O", "Gen1a-to-Shaft");
linkPorts( "Turbine_Shaft.Sh1_Ib"    , "Gen1b.Sh_O", "Gen1b-to-Shaft");
linkPorts( "Turbine_Shaft.Sh2_I"    , "Gen2.Sh_O", "Gen2-to-Shaft");
